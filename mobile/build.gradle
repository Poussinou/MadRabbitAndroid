final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 1 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits


apply plugin: 'com.android.application'

allprojects {
    repositories {
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:25.2.0'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:design:25.2.0'
    compile 'com.google.android.gms:play-services-base:10.2.0'
    compile 'com.google.android.gms:play-services-gcm:10.2.0'
    compile 'com.onesignal:OneSignal:3.4.3@aar'
    compile 'com.devbrackets.android:exomedia:3.1.1'
    compile 'com.github.delight-im:Android-AdvancedWebView:v3.0.0'
}


android
        {
            compileSdkVersion 25
            buildToolsVersion "25.0.2"

            defaultConfig
                    {
                        applicationId "com.madrabbit"
                        minSdkVersion 16
                        targetSdkVersion 25
                        versionCode VERSION_MAJOR * 1000 + VERSION_MINOR * 100 + VERSION_PATCH * 10 + VERSION_BUILD
                        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}"
                        manifestPlaceholders = [manifestApplicationId          : "${applicationId}",
                                                onesignal_app_id               : "${onesignal_app_id}",
                                                onesignal_google_project_number: "${onesignal_google_project_number}"]
                    }

            signingConfigs
                    {
                        release
                                {
                                    // passwords and alias are obtained via askForPasswords task
                                    storeFile file("../${project.property('keystore.file')}")
                                    storePassword ""
                                    keyAlias ""
                                    keyPassword ""
                                }
                    }

            buildTypes
                    {
                        debug
                                {
                                    buildConfigField "boolean", "LOGS", "true"

                                    versionNameSuffix "-debug"
                                }

                        release
                                {
                                    buildConfigField "boolean", "LOGS", "false"

                                    signingConfig signingConfigs.release
                                    zipAlignEnabled true
                                    minifyEnabled false
                                    shrinkResources false
                                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                                }
                    }

            applicationVariants.all
                    {
                        variant ->
                            if (variant.buildType.name.equals("release")) {
                                variant.outputs.each
                                        {
                                            output ->
                                                def outputFile = output.outputFile
                                                def date = new Date()
                                                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                                                    def fileName = outputFile.name.replace(
                                                            "mobile",
                                                            "madrabbit-" + defaultConfig.versionName + "-" + defaultConfig.versionCode + "-" + date.format('yyyyMMdd'))
                                                    output.outputFile = new File((String) outputFile.parent, (String) fileName)
                                                }
                                        }
                            }
                    }
        }


task askForPasswords <<
        {
            def storePass
            def keyAlias
            def keyPass

            def keystorePropertiesFile = new File((String) project.property("keystore.properties"))

            if (project.hasProperty("keystore.properties") && keystorePropertiesFile.exists()) {
                println "Loading keystore passwords from property file..."
                Properties properties = new Properties()
                properties.load(new FileInputStream(keystorePropertiesFile))
                storePass = properties['keystore.store.password']
                keyAlias = properties['keystore.key.alias']
                keyPass = properties['keystore.key.password']
            } else {
                println "Getting keystore passwords from user input..."
                storePass = new String(System.console().readPassword("\nStore password: "))
                keyAlias = new String(System.console().readLine("Key alias: "))
                keyPass = new String(System.console().readPassword("Key password: "))
            }

            android.signingConfigs.release.storePassword = storePass
            android.signingConfigs.release.keyAlias = keyAlias
            android.signingConfigs.release.keyPassword = keyPass
        }


tasks.whenTaskAdded
        {
            theTask ->
                if (theTask.name.equals("packageRelease")) {
                    theTask.dependsOn "askForPasswords"
                }
        }